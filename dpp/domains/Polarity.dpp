Lattice Polarity = Top | Bot | Odd | Even;

Order Polarity(p1, p2) = (p1, p2) match {
    case (Bot, _) => true;
    case (Odd, Odd) => true;
    case (Even, Even) => true;
    case (_, Top) => true;
    case _ => false;
}

Join Polarity(p1, p2) = (p1, p2) match {
    case (p, Bot) => p;
    case (Bot, p) => p;

    case (Odd, Odd) => Odd;
    case (Even, Even) => Even;

    case _ => Top;
}

Function lift(i: Int) =
    if (i % 2 != 0)
        Odd
     else
        Even;

@Strict
Function sum(p1: Polarity, p2: Polarity) = (p1, p2) match {
    case (Bot, _) => Bot;
    case (_, Bot) => Bot;

    case (Odd, Odd) => Even;
    case (Even, Even) => Even;

    case (Odd, Even) => Odd;
    case (Even, Odd) => Odd;

    case _ => Top;
}